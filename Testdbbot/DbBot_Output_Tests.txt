*** Settings ***

Documentation                   This Robot test script will automatically test DbBot using the test output.xml files...
...                             To use, please configure the variables below
...                             ...
...                             NOTE: Before running the tests, it is advised that you change the database to a test
...                             database -- so as to not affect your real results.
Library                         OperatingSystem


*** Variables ***


${path_to_dbbot}                C:\\GitHome\\TestAssetSourceControlManagement\\mssql_dbbot
${path_to_dbbot_testdata}       ${path_to_dbbot}\\testdata


*** Test Cases ***


Illegal Characters
    [Documentation]                                 Input output file containing illegal characters
    ${console_output} =                             Console Output For Output File in Sub-Directory  illegal_chars
    Database Commit Should Have Succeeded           ${console_output}

Invalid XML
    [Documentation]                                 Input output file containing invalid XML
    ${console_output} =                             Console Output For Output File in Sub-Directory  invalid_xml
    Parse Error Should Be Present                   ${console_output}

Multiple Suites
    [Documentation]                                 Input output file containing multiple suites
    ${console_output} =                             Console Output For Output File in Sub-Directory  multiple
    Database Commit Should Have Succeeded           ${console_output}

One Suite
    [Documentation]                                 Input output file containing a single suite
    ${console_output} =                             Console Output For Output File in Sub-Directory  one_suite
    Database Commit Should Have Succeeded           ${console_output}

Rebot
    [Documentation]                                 Input output file generated by Rebot
    ${console_output} =                             Console Output For Output File in Sub-Directory  rebot
    Database Commit Should Have Succeeded           ${console_output}

Large XML
    [Documentation]                                 Input large output file containing 20,000 tests
    ${console_output} =                             Console Output For Output File in Sub-Directory  large_xml
    Database Commit Should Have Succeeded           ${console_output}


*** Keywords ***


Console Output For Output File in Sub-Directory
    [Arguments]                 ${subDir}
    ${output} =                 Run                         python ${path_to_dbbot}\\dbbot.py --verbose --also-keywords -m -c -f ${path_to_dbbot_testdata}\\${subDir}\\test_output.xml
    [Return]                    ${output}

Database Commit Should Have Succeeded
    [Arguments]                 ${console_output}
    Should Contain              ${console_output}           Committing changes into database

Parse Error Should Be Present
    [Arguments]                 ${console_output}
    Should Contain              ${console_output}           ParseError
